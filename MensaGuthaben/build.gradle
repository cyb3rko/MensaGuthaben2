plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    namespace 'de.yazo_games.mensaguthaben'

    compileSdk 33
    defaultConfig {
        applicationId 'de.yazo_games.mensaguthaben'
        minSdk 23
        targetSdk 33
        versionCode 1
        versionName '1.0.0'
    }
    
    signingConfigs {
        release {
            // We can leave these in environment variables
            storeFile file(System.getenv("KEYSTORE") ?: "keystore")
            keyAlias System.getenv("KEY_ALIAS") ?: "jakobwenzel"

            // These two lines make gradle believe that the signingConfigs
            // section is complete. Without them, tasks like installRelease
            // will not be available!
            storePassword "notYourRealPassword"
            keyPassword "notYourRealPassword"
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
}

task askForPassword {
    doLast {
        def storePw = PasswordPrompt.readPassword("Keystore password") ?: "invalid" //If it's null, gradle complains


        android.signingConfigs.release.storePassword = storePw
        android.signingConfigs.release.keyPassword = storePw
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.core:core-ktx:1.10.0'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'com.google.android.material:material:1.9.0'
}

//tasks.whenTaskAdded { theTask ->
//    if (theTask.name.equals("packageRelease")) {
//        theTask.dependsOn "askForPassword"
//    }
//}

